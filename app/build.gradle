apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "kotlin-kapt"
apply plugin: 'androidx.navigation.safeargs'

// Ref: https://qiita.com/gfx/items/fd8e201f9dc71518abb8
def gitRevisionHash = 'git rev-parse --short HEAD'.execute(null, rootDir).in.text.trim()
// Ref: https://gist.github.com/paramsen/4a92c9230d799caeb3b90897da27a80c
def gitCommitCount = "git rev-list HEAD --count".execute().text.trim()
// Ref: https://stackoverflow.com/questions/15061277/how-can-i-get-the-current-git-branch-in-gradle
def gitBranchName = "git rev-parse --abbrev-ref HEAD".execute().text.replace("/", "_").trim()

android {
    compileSdkVersion rootProject.ext.compileSdkVer

    defaultConfig {
        applicationId "jp.co.zeppelin.nec.hearable"
        minSdkVersion rootProject.ext.minSdkVer
        targetSdkVersion rootProject.ext.targetSdkVer
        versionCode 1
        versionName "1.0.$gitCommitCount"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    // app-debug.apk
    // Ref: https://developer.android.com/studio/build/gradle-tips#configure-dynamic-version-codes
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "app_${gitBranchName}_${gitRevisionHash}-${versionName}-${output.apkData.fullName}.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(":domain")
    implementation project(":qrcodescan")
    implementation project(":necsdkwrapper")
    implementation project(":data")

    api "com.wang.avi:library:2.1.3"

    implementation "pub.devrel:easypermissions:3.0.0"

    // https://developer.android.com/guide/topics/ui/settings
    implementation "androidx.preference:preference:1.1.0"

    implementation "com.airbnb.android:lottie:3.3.0"

    // https://developer.android.com/topic/libraries/architecture/coroutines
    // For viewModelScope
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    // For liveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    // for lifecycleScope
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

    // https://square.github.io/leakcanary/getting_started/
    // TODO: Disabled to make logcat easier to read; should be re-enabled
//    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.0"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$aacNavVer"
    implementation "androidx.navigation:navigation-ui-ktx:$aacNavVer"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVer"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "androidx.core:core-ktx:$ktxVer"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
